Prog -> Dec Prog1
Prog1 -> ε
Prog1 -> Prog
Dec -> ClassDec
Dec -> InterDec
Dec -> Var Dec1
Dec -> tVoid tId FuncDec
Dec1 -> tSemiColon
Dec1 -> FuncDec
Var -> Type tId
Type -> tInt Type1
Type -> tDouble Type1
Type -> tBool Type1
Type -> tString Type1
Type -> tUserType Type1
Type1 -> ε
Type1 -> tBracketLeft tBracketRight Type1
FuncDec -> tParLeft Formals tParRight StmtBlock
Formals -> ε
Formals -> Formals1
Formals1 -> Var Formals2
Formals2 -> ε
Formals2 -> tComma Formals1
StmtBlock -> tBraceLeft StmtBlock1 tBraceRight
StmtBlock1 -> StatementList
StatementList -> Stmt StatementList
StatementList -> ε
StmtBlock1 -> Var tSemiColon StmtBlock1
Stmt -> ConditionStmt
Stmt -> OtherStmt
ConditionStmt -> tIf tParLeft Expr tParRight tBraceLeft Stmt tBraceRight OpTail
OpTail -> tElse tBraceLeft Stmt tBraceRight
OpTail -> ε
OtherStmt -> WhileStmt
OtherStmt -> ForStmt
OtherStmt -> BreakStmt
OtherStmt -> ReturnStmt
OtherStmt -> PrintStmt
OtherStmt -> StmtBlock
OtherStmt -> ExprAssign tSemiColon
OtherStmt -> tSemiColon
WhileStmt -> tWhile tParLeft Expr tParRight Stmt
ForStmt -> tFor tParLeft ExprAssignOrEmpty tSemiColon Expr tSemiColon ExprOrEmpty tParRight Stmt
ReturnStmt -> tReturn ExprOrEmpty tSemiColon
BreakStmt -> tBreak tSemiColon
PrintStmt -> tPrint tParLeft Expr PrintOtherStmt
PrintOtherStmt -> tParRight tSemiColon
PrintOtherStmt -> tComma Expr PrintOtherStmt
ClassDec -> tClass tUserType ClassDec1
ClassDec1 -> tExtends tUserType ClassDec2
ClassDec1 -> ClassDec2
ClassDec2 -> tImplements tUserType Implements tBraceLeft Field tBraceRight
ClassDec2 -> tBraceLeft Field tBraceRight
Implements -> ε
Implements -> tComma tUserType Implements
Field -> ε
Field -> Var Dec1 Field
InterDec -> tInterface tUserType tBraceLeft Prototype tBraceRight
Prototype -> ε
Prototype -> Var tParLeft Formals tParRight tSemiColon
Prototype -> tVoid tId tParLeft Formals tParRight tSemiColon
ExprAssign -> LValue Variable tAssignment Expr
ExprAssignOrEmpty -> ExprAssign
ExprAssignOrEmpty -> ε
Expr -> RelOp LogicOp
LogicOp -> ε
LogicOp -> tOr RelOp LogicOp
LogicOp -> tAnd RelOp LogicOp
RelOp -> PlusSubOp RelOp1
RelOp1 -> ε
RelOp1 -> tLess PlusSubOp RelOp1
RelOp1 -> tLessEqual PlusSubOp RelOp1
RelOp1 -> tGreater PlusSubOp RelOp1
RelOp1 -> tGreaterEqual PlusSubOp RelOp1
RelOp1 -> tEqual PlusSubOp RelOp1
RelOp1 -> tDiff PlusSubOp RelOp1
PlusSubOp -> MulDivModOp PlusSubOp1
PlusSubOp1 -> ε
PlusSubOp1 -> tPlus MulDivModOp PlusSubOp1
PlusSubOp1 -> tMinus MulDivModOp PlusSubOp1
MulDivModOp -> UnaryOp MulDivModOp1
MulDivModOp1 -> ε
MulDivModOp1 -> tMulti UnaryOp MulDivModOp1
MulDivModOp1 -> tDiv UnaryOp MulDivModOp1
MulDivModOp1 -> tMod UnaryOp MulDivModOp1
UnaryOp -> tNot UnaryOp
UnaryOp -> tMinus UnaryOp
UnaryOp -> Term
Term -> LValue CallOrVariable
Term -> Constant
Term -> tReadInteger tParLeft tParRight
Term -> tReadLine tParLeft tParRight
Term -> tNew tParLeft tId tParRight
Term -> tNewArray tParLeft Expr tComma Type tParRight
Term -> tParLeft Expr tParRight
CallOrVariable -> Call
CallOrVariable -> Variable
Call -> tParLeft Actual tParRight CallVariable
CallVariable -> ε
CallVariable -> VariableNotEmpty
VariableNotEmpty -> tBracketLeft Expr tBracketRight CallAfterVariable
VariableNotEmpty -> tDot LValue CallAfterVariable
CallAfterVariable -> ε
CallAfterVariable -> VariableNotEmpty
CallAfterVariable -> Call
Variable -> ε
Variable -> tBracketLeft Expr tBracketRight Variable
Variable -> tDot LValue Variable
LValue -> tId
LValue -> tThis tDot tId
ExprOrEmpty -> ε
ExprOrEmpty -> Expr
ExprSeq -> ε
ExprSeq -> tComma Expr ExprSeq
Actual -> ε
Actual -> Expr ExprSeq
Constant -> tIntConstant
Constant -> tDoubleConstant
Constant -> tTrue
Constant -> tFalse
Constant -> tStringConstant
Constant -> tNull